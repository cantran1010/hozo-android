buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'io.fabric.tools:gradle:1.22.1'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'realm-android'

int extractVersionCode() {
    def stdout = new ByteArrayOutputStream()
    exec {
        workingDir projectDir
        executable 'git'
        args 'rev-list', 'HEAD', '--count'
        standardOutput = stdout
    }
    return stdout.toString().trim().toInteger()
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"
    defaultConfig {
        applicationId "vn.tonish.hozo"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode extractVersionCode()
        versionName "1.0.1"
        multiDexEnabled true
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    signingConfigs {
        debug {
            storeFile file('../keystore_debug/debug.keystore')
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storePassword 'android'
        }
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        debug {
            buildConfigField "String", "BASE_URL", '"https://staging-api.hozo.vn/v1/"'
            debuggable true
        }
        release {
            buildConfigField "String", "BASE_URL", '"https://api.hozo.vn/v1/"'
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/NOTICE' // will not include NOTICE file
        exclude 'META-INF/LICENSE' // will not include LICENSE file
    }
    lintOptions {
        disable 'InvalidPackage'
    }
}


dependencies {
    compile('com.crashlytics.sdk.android:crashlytics:2.6.7@aar') {
        transitive = true;
    }
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'com.google.firebase:firebase-messaging:10.2.6'
    compile 'com.theartofdev.edmodo:android-image-cropper:2.4.2'
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'
    compile 'com.google.android.gms:play-services-maps:10.2.6'
    compile 'com.google.android.gms:play-services-places:10.2.6'
    compile 'com.google.android.gms:play-services-location:10.2.6'
    compile 'com.android.support:multidex:1.0.1'
    compile files('libs/glide-3.8.0.jar')
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile('com.squareup.okhttp:okhttp-urlconnection:2.3.0') {
        exclude group: 'com.squareup.okhttp', module: 'okhttp'
    }
    compile('com.squareup.okhttp:okhttp:2.3.0') {
        exclude group: 'com.squareup.okio', module: 'okio'
    }
}
apply plugin: 'com.google.gms.google-services'

