buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
        mavenCentral()
    }
    dependencies {
        classpath 'io.fabric.tools:gradle:1.22.2'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    google()
}

apply plugin: 'realm-android'

int extractVersionCode() {
    def stdout = new ByteArrayOutputStream()
    exec {
        workingDir projectDir
        executable 'git'
        args 'rev-list', 'HEAD', '--count'
        standardOutput = stdout
    }
    return stdout.toString().trim().toInteger()
}

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'
    defaultConfig {
        applicationId "vn.tonish.hozo"
        minSdkVersion 15
        targetSdkVersion 26
        versionCode extractVersionCode()
        versionName "2.0.1"
        multiDexEnabled true
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    signingConfigs {
        debug {
            storeFile file('../keystore_debug/debug.keystore')
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storePassword 'android'
        }
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        debug {
            buildConfigField "String", "BASE_URL", '"https://staging-api.hozo.vn/"'
            buildConfigField "String", "SHARE_URL", '"https://staging.hozo.vn/share/tasks/"'
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            buildConfigField "String", "BASE_URL", '"https://api.hozo.vn/"'
            buildConfigField "String", "SHARE_URL", '"https://hozo.vn/share/tasks/"'
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/NOTICE' // will not include NOTICE file
        exclude 'META-INF/LICENSE' // will not include LICENSE file
    }
    lintOptions {
        disable 'InvalidPackage'
    }
}
configurations.all {
    resolutionStrategy.eachDependency { details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion '25.3.1'
            }
        }
    }
}


dependencies {
    compile('com.crashlytics.sdk.android:crashlytics:2.8.0@aar') {
        transitive = true
    }
    compile 'com.facebook.android:account-kit-sdk:4.28.0'
    compile 'com.googlecode.libphonenumber:libphonenumber:8.5.2'
    compile 'com.android.support:appcompat-v7:26.1.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.android.support:design:26.1.0'
    compile 'com.android.support:cardview-v7:26.1.0'
    compile 'com.android.support:recyclerview-v7:26.1.0'
    compile 'com.theartofdev.edmodo:android-image-cropper:2.4.2'
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'
    compile 'com.google.android.gms:play-services-maps:11.8.0'
    compile "com.google.android.gms:play-services-auth:11.8.0"
    compile 'com.google.android.gms:play-services-places:11.8.0'
//    compile 'com.google.android.gms:play-services-location:11.8.0'
    compile 'com.google.firebase:firebase-messaging:11.8.0'
    compile 'com.google.firebase:firebase-database:11.8.0'
    compile 'com.facebook.android:facebook-android-sdk:4.28.0'
    compile 'com.android.support:multidex:1.0.2'
    compile files('libs/glide-3.8.0.jar')
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile('com.squareup.okhttp:okhttp-urlconnection:2.3.0') {
        exclude group: 'com.squareup.okhttp', module: 'okhttp'
    }
    compile('com.squareup.okhttp:okhttp:2.3.0') {
        exclude group: 'com.squareup.okio', module: 'okio'
    }
    compile 'me.relex:circleindicator:1.2.2@aar'
    implementation 'com.android.support:exifinterface:26.1.0'
}
apply plugin: 'com.google.gms.google-services'

